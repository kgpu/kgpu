plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'java'
}
repositories {
    mavenCentral()
}

group "io.github.kgpu"
version '0.1.0'

kotlin {
    jvm()
    js(){
        browser {
            webpackTask {
                sourceMaps = false
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies{
                implementation(project(":"))
                implementation kotlin('stdlib-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

task runJvmExample(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "DesktopExampleKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}

task buildWeb(type: Copy, dependsOn: assemble) {
    from("$buildDir/distributions") {
        include("**.**")
    }
    into("$buildDir/website")
}

task startExampleServer(){
    def port = 8080;
    def path = "$buildDir/website"

    doLast{
        def server = new SimpleHttpFileServerFactory().start(new File(path), port)

        println "Server started in directory " + server.getContentRoot()
        println "Link: http://localhost:" + server.getPort() + "/index.html\n\n"
    }
}